classe cmd_levantar
herda comando_comum
const posic = 5
const txtajuda = "\b\c3Levantar\b\nSintaxe: LEVANTAR\nFica em pé."

func escr
# Checa posição
  casovar arg0.posicao
  casose "5" # Descansando
  casose "6" # Sentado
    sair
  casose "8" # Em pé
    ret $mens.mp("\c9Você já está em pé.\b", arg0)
  casose # Outras posições
    ret arg0.msg(arg0.erropos)
  casofim
# Ficar em pé
  listaobj l
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_levantar(arg0), nulo
  efim
  arg0.poslugar.remove
  arg0.posicao = 8
  $mens.p(arg0)
  $mens.mtodos1("$P levantou-se.")
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_levantou(arg0)
  efim


classe cmd_sentar
herda comando_comum
const posic = 5
const txtajuda = "\b\c3Sentar\b\n\
Sintaxe: SENTAR [objeto]\n\
Senta-se no chão. Com o nome de um objeto, tenta sentar no objeto."

func escr
  ref r
# Checa posição
  casovar arg0.posicao
  casose "5" # Descansando
    r = arg0.poslugar.objlista
    sair
  casose "6" # Sentado
    ret $mens.mp("\c9Você já está sentad$s.\b", arg0)
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msg(arg0.erropos)
  casofim
# Obtém aonde quer se sentar
  se arg1
    listaitem i # Para procurar o item
    nomeobj n # Para reconhecer os itens
    n.ini(arg1, 1)
    epara i = arg0.dono.dentro1.ini, i, i.depois
      sair i.obj.visivel(arg0) && n.nome(i.obj.ident, i.obj.objtot)
    efim
    ret r && r != i.obj, $mens.mp("\c9Talvez você deva se levantar primeiro.\b", arg0)
    r = i.obj
  fimse
  listaobj l
# Sentar em algum objeto
  se r
    se !r.i_movel(arg0)
      ret $mens.mp("Não é possível descansar $e $o.", arg0, nulo, r)
    senao r != arg0.poslugar.objlista && r.i_lugar <= r.persolugar.total
      ret $mens.mp("Não cabe mais ninguém $e $o.", arg0, nulo, r)
    fimse
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_sentarobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_sentarobj(arg0, r, r), nulo
    efim
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 6
    $mens.p(arg0, nulo, r)
    $mens.mtodos1("$P sentou-se $e $o.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_sentouobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_sentouobj(arg0, r, r)
    efim
# Sentar-se num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp("Você não vê $m.", arg0)
# Sentar-se no chão
  senao
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_sentarchao(arg0), nulo
    efim
    arg0.poslugar.remove
    arg0.posicao = 6
    $mens.p(arg0)
    $mens.mtodos1("$P sentou-se.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_sentouchao(arg0)
    efim
  fimse


classe cmd_descansar
herda comando_comum
const posic = 5
const txtajuda = "\b\c3Descansar\b\n\
Sintaxe: DESCANSAR [objeto]\n\
Descansa no chão. Com o nome de um objeto, tenta descansar no objeto."

func escr
  ref r
# Checa posição
  casovar arg0.posicao
  casose "5" # Descansando
    ret $mens.mp("\c9Você já está descansando.\b", arg0)
  casose "6" # Sentado
    r = arg0.poslugar.objlista
    sair
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msg(arg0.erropos)
  casofim
# Obtém aonde quer descansar
  se arg1
    listaitem i # Para procurar o item
    nomeobj n # Para reconhecer os itens
    n.ini(arg1, 1)
    epara i = arg0.dono.dentro1.ini, i, i.depois
      sair i.obj.visivel(arg0) && n.nome(i.obj.ident, i.obj.objtot)
    efim
    ret r && r != i.obj, $mens.mp("\c9Talvez você deva se levantar primeiro.\b", arg0)
    r = i.obj
  fimse
  listaobj l
# Descansar em algum objeto
  se r
    se !r.i_movel(arg0)
      ret $mens.mp("Não é possível descansar $e $o.", arg0, nulo, r)
    senao r != arg0.poslugar.objlista && r.i_lugar <= r.persolugar.total
      ret $mens.mp("Não cabe mais ninguém $e $o.", arg0, nulo, r)
    fimse
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_descansarobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_descansarobj(arg0, r, r), nulo
    efim
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 5
    $mens.p(arg0, nulo, r)
    $mens.mtodos1("$P descansa $e $o.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_descansouobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_descansouobj(arg0, r, r)
    efim
# Descansar num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp("Você não vê $m.", arg0)
# Descansar no chão
  senao
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_descansarchao(arg0), nulo
    efim
    arg0.poslugar.remove
    arg0.posicao = 5
    $mens.p(arg0)
    $mens.mtodos1("$P descansa.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_descansouchao(arg0)
    efim
  fimse
