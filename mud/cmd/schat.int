classe h_cmd_schat
herda comando_comum
const posic = 0
const objcmd = v_tipo ? este
const objnivel = 4
const txtajuda = v_tipo ? txtajuda1 + m_ajuda1 + (v_tipo == 1 ? "\n" + m_ajuda2)
const txtajuda1 = "\b\c3Schat\b\nSintaxe: SCHAT <comando social>\n"
#
const v_tipo = 2
const v_visivel = 1
const m_ajuda1 = "Executa um comando social no canal CHAT. Para saber quais comandos sociais\n\
existem, basta teclar Social. Para habilitar ou desabilitar o canal CHAT,\n\
use o comando CONFIG."
const m_ajuda2 = "Nota: quando o jogo é ativado em um chat, mensagens de uma sala do chat\n\
não irão para as outras salas."
const m_nivel = "Para usar o comando SCHAT você precisa chegar no nível $m."
const v_nivel = 0
const m_desab = "Canal CHAT está desabilitado; para habilitar tecle CONFIG +CHAT"
const m_sem_args = "Digite o comando social após SCHAT."
const m_sem_social = "Comando social inexistente: $m"
const m_sem_schat = "Digite alguma coisa após Schat $m"
const m_ignorar = "Você está ignorando %c."
const m_ignorado = "$C ignora suas palavras."
const m_condicoes1 = "Você não está em condições."
const m_condicoes2 = "$A não está em condições."
const m_msg1 = "(chat) $m"
const m_msg2 = "(chat) $m"
const m_msg3 = "(chat) $m"
const admordem = "v_tipo v_visivel m_ajuda1 m_ajuda2 m_nivel v_nivel m_desab\n\
m_sem_args m_sem_social m_sem_schat m_ignorar m_condicoes1 m_condicoes2\n\
m_msg1 m_msg2 m_msg3"
#
ref obj0 # Personagem
ref obj1 # Alvo
ref obj2 # Item
const mm_@ = "$"
txt512 mm_m

func escr
  prog p
  p.iniclasse("soc_" + txt1(arg1))
  refvar s = $[p.texto]
# Checa se pode usar o comando
  se arg2.c_chat
    ret $mensjog.msg(arg2.c_chat, arg0)
  senao arg2.c_social
    ret $mensjog.msg(arg2.c_social, arg0)
  senao arg0.pnivel < v_nivel
    $mens.mens = v_nivel
    $mens.mp(m_nivel, arg0)
    ret
  senao !arg0.jogconfig.4
    ret $mens.mp(m_desab, arg0)
  senao !arg1
    ret $mens.mp(m_sem_args, arg0)
  senao !s
    $mens.mens = txt1(arg1)
    ret $mens.mp(m_sem_social, arg0)
  fimse
# Obtém a lista de jogadores em ljog
  listaobj ljog
  se v_tipo == 2 # Escrever para todos
    indiceitem item
    epara item.ini("un "), txt1(item.txt) == "un", item.depois
      item.obj.jogconfig.4 && ljog.addfim(item.obj.perso)
    efim
  senao # Escrever para quem estiver na mesma sala
    listaobj l
    epara arg0.sock.salaobj(l), l, l.ini.remove
      l.objini.jogconfig.4 && ljog.addfim(l.objini.perso)
    efim
  fimse
# Acerta variáveis
  $mens.mens = txt2(arg1)
  listaobj l
# Schat sem argumentos
  se !txt2(arg1)
    se !s.sem1
      $mens.mens = txt(s, 4)
      $mens.mp(m_sem_schat, arg0)
      ret
    fimse
    perso(arg0)
    mtodos(ljog, s.sem1, s.sem2, s.sem2)
    ret
  fimse
# Schat com argumentos
  nomeobj n
  ref r
  n.ini(txt2(arg1), 1)
  s.perso1 && l.addfim(ljog)
  s.item1 && l.addfim(arg0.dono.dentro1, arg0.dentro1)
  epara nulo, l, l.ini.remove
    r = l.objini
    continuar r.jogconfig.17 || !n.nome(r.ident)
    se r.var.u_[arg0.nome]
      ret $mens.mp(m_ignorado, arg0, r)
    senao arg0.var.u_[r.nome]
      ret $mens.mp(m_ignorar, arg0, r)
    senao r == arg0 && s.auto1
      ret r.posic < s.alvopos, $mens.mp(m_condicoes1, arg0, r)
      perso(arg0)
      mtodos(ljog, s.auto1, s.auto2, s.auto2)
    senao r.perso
      $mens.p(arg0, r)
      perso(arg0, r)
      ret r.posic < s.alvopos, $mens.mp(m_condicoes2, arg0, r)
      mtodos(ljog, s.perso1, s.perso2, s.perso3)
    senao
      perso(arg0, nulo, r)
      mtodos(ljog, s.item1, s.item2, s.item2)
    fimse
    ret
  efim
  $mens.mp(s.invalido, arg0)

func perso # Define os personagens
  obj0 = arg0, obj1 = arg1, obj2 = arg2

func mtodos # Envia mensagem
# arg0 = listaobj com os personagens que receberão as mensagens
# arg1 = mensagem para o personagem principal
# arg2 = mensagem para o personagem alvo
# arg3 = mensagem para os demais
  refvar mens = v_visivel ? $mensvis : $mens
  mens.p(obj0, obj1, obj2)
  arg1 && !obj0.var.u_[obj0.nome] && mens.msg(conv(arg1, m_msg1), obj0)
  arg2 && !obj1.var.u_[obj0.nome] && mens.msg(conv(arg2, m_msg2), obj1)
  ret !arg2, nulo
  refvar texto = conv(arg3, m_msg3)
  listaobj l0
  l0.addfim(arg0)
  epara l0.remove(obj0, obj1), l0, l0.ini.remove
    !l.objini.var.u_[obj0.nome] && mens.msg(texto, l0.objini)
  efim

func conv # Junta mensagem do social com a mensagem do schat
  mm_m = arg0
  ret vartroca(txttroca(arg1, "$$", "$@"), "$", "mm_")


classe m_cmd_schat
herda comando_menu_cmd
#
const nome1 = "Comando Schat"
const tipo1 = "opc_opcao"
const clas1 = "cmd_schat"
const vari1 = "v_tipo"
const extr1 = "Desabilitado\nBots: Somente na mesma sala\nPara todo o MUD"
#
const nome2 = "Visível"
const tipo2 = "opc_opcao"
const clas2 = "cmd_schat"
const vari2 = "v_visivel"
const extr2 = "Pode aparecer alguém e alguma coisa\nPersonagens/itens sempre visíveis"
#
const nome3 = "Ajuda"
const tipo3 = "opc_texto"
const clas3 = "cmd_schat"
const vari3 = "m_ajuda1"
#
const nome4 = "Ajuda: sala do chat"
const tipo4 = "opc_texto"
const clas4 = "cmd_schat"
const vari4 = "m_ajuda2"
#
const nome5 = "Nível mínimo"
const tipo5 = "opc_numero"
const clas5 = "cmd_schat"
const vari5 = "v_nivel"
#
const nome6 = "Nível insuficiente"
const tipo6 = "opc_linha"
const clas6 = "cmd_schat"
const vari6 = "m_nivel"
#
const nome7 = "Desabilitado"
const tipo7 = "opc_linha"
const clas7 = "cmd_schat"
const vari7 = "m_desab"
#
const nome8 = "Sem argumentos"
const tipo8 = "opc_linha"
const clas8 = "cmd_schat"
const vari8 = "m_sem_args"
#
const nome9 = "Sem comando social"
const tipo9 = "opc_linha"
const clas9 = "cmd_schat"
const vari9 = "m_sem_social"
#
const nome10 = "Argumentos Schat"
const tipo10 = "opc_linha"
const clas10 = "cmd_schat"
const vari10 = "m_sem_schat"
#
const nome11 = "Ignorando"
const tipo11 = "opc_linha"
const clas11 = "cmd_schat"
const vari11 = "m_ignorar"
#
const nome12 = "Ignorado"
const tipo12 = "opc_linha"
const clas12 = "cmd_schat"
const vari12 = "m_ignorado"
#
const nome13 = "Perso sem condições"
const tipo13 = "opc_linha"
const clas13 = "cmd_schat"
const vari13 = "m_condicoes1"
#
const nome14 = "Alvo sem condições"
const tipo14 = "opc_linha"
const clas14 = "cmd_schat"
const vari14 = "m_condicoes2"
#
const nome15 = "Mensagem perso"
const tipo15 = "opc_linha"
const clas15 = "cmd_schat"
const vari15 = "m_msg1"
#
const nome16 = "Mensagem alvo"
const tipo16 = "opc_linha"
const clas16 = "cmd_schat"
const vari16 = "m_msg2"
#
const nome17 = "Mensagem outros"
const tipo17 = "opc_linha"
const clas17 = "cmd_schat"
const vari17 = "m_msg3"
