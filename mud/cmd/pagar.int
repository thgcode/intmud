classe cmd_pagar
herda comando_comum, comum_negociar
const txtajuda = "\b\c3Pagar\b\n\
Sintaxe: PAGAR [item]\n\
Sem argumentos mostra a lista de casas cujas taxas de manutenção podem\n\
ser pagas. Seguido de um nome ou número paga a taxa."
const posic = 5

func escr
  arg1 ? escr2(arg0, arg1) : escr1(arg0)

func escr1 # Consultar a lista de taxas
  se intpos(config:casatempotaxa) == 0
    ret arg0.msg("Não há taxa de manutenção das casas.")
  fimse
  int32 valor # Valor do item
  real2 lucro # Para calcular o lucro do vendedor
  txt200 lin
  int32 linha
  textotxt t
  listaobj l
  linha = 1, l.addfim(arg0.dono.dentro2)
  epara l.remove(arg0), l, l.ini.remove
    refvar p = ref(l.objini) # Personagem que está sendo verificado
    continuar !p.taxacasa
    lin = negociar_msg(arg0, p)
    continuar lin == "\b"
    ret lin, arg0.msg(lin)
    se 1
      listaobj e
      lin = ""
      epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
        lin = e.objini.cmd_negociar(arg0, p, "pagar")
        sair lin
      efim
      se lin
        t.addfim(txtcopiamai(p.descnome, "A") + ": " + lin)
        continuar
      fimse
    fimse
    t.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " informa:\b")
    lucro = (100 + p.taxavenda) / 100
# Informa casas
    se 1
      textotxt t2
      indiceitem item
      datahora hora
      hora.agora
      int1 mudou
      epara t2.addfim(p.taxacasa), t2.linhas, t2.remove
        refvar casa = item.obj("cj " + txt2(arg0.sock.cnome) + " " + t2.ini.texto)
        continuar !casa
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.")
        lin += txtcopiamai(casa.nome, "A")
        lin += txtesp(40 - inttotal(lin))
        valor = hora.numdias - casa.diataxa
        se valor < 0
          refvar v = -12 * valor - intdiv(hora.hora / 2)
          lin += v == 1 ? "Em 1 dia MUD" : "Em " + v + " dias MUD"
          se v < 12
            lin += " (" + v * 2 + " horas)"
          senao v == 12
            lin += " (1 dia)"
          senao v < 24
            lin += " (1 dia e " + (v * 2 - 24) + " horas)"
          senao v % 12 == 0
            lin += " (" + intdiv(v / 12) + " dias)"
          senao
            lin += " (" + intdiv(v / 12) + " dias e " + v % 12 * 2 + " horas)"
          fimse
          t.addfim(lin)
        senao
          se config:casatempotaxa
            valor = intdiv(valor / config:casatempotaxa) + 1
          senao
            valor = 0
          fimse
          valor *= lucro * casa.valor * config:casataxa / 100
          t.addfim(lin + "$ " + valor)
        fimse
        mudou = 1
      efim
      !mudou && t.addfim("Você não deve nada.")
    fimse
  efim
# Envia mensagens
  ret t.linhas, arg0.msg2(t.remove(1000))
  arg0.msg("Ninguém cobra taxas aqui.")

func escr2 # Consultar um item
  int32 valor # Valor do item
  real2 lucro # Para calcular o lucro do vendedor
  txt200 lin
  int32 linha
  listaobj l
  nomeobj n
  linha = 0, l.addfim(arg0.dono.dentro2), n.ini(arg1, 1)
  epara l.remove(arg0), l, l.ini.remove
    refvar p = ref(l.objini) # Personagem que está sendo verificado
    continuar !p.taxacasa
    lin = negociar_msg(arg0, p)
    continuar lin == "\b"
    ret lin, arg0.msg(lin)
    listaobj e
    epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
      sair e.objini.cmd_negociar(arg0, p, "pagar")
    efim
    continuar e
    lucro = (100 + p.taxavenda) / 100
# Pagar casas
    se 1
      textotxt t2
      indiceitem item
      datahora hora
      hora.agora
      epara t2.addfim(p.taxacasa), t2.linhas, t2.remove
        refvar casa = item.obj("cj " + txt2(arg0.sock.cnome) + " " + t2.ini.texto)
        continuar !casa
        continuar txt(++linha) != arg1 && !n.nome(casa.ident, 1)
        valor = hora.numdias - casa.diataxa
        se valor < 0
          ret arg0.msg(txtcopiamai(p.descnome, "A") + " informa que a taxa já está paga.")
        senao config:casatempotaxa
          refvar total = intdiv(valor / config:casatempotaxa) + 1
          valor = total * lucro * casa.valor * config:casataxa / 100
          se arg0.var.z_moedas_ < valor
            ret arg0.msg("Você não tem " + valor + (valor == 1 ? " moeda." : " moedas."))
          fimse
          casa.diataxa += total * config:casatempotaxa
          casa.tempotaxa = 1
          casa.objmudou = 1
          arg0.var.z_moedas_ -= valor
        senao
          casa.diataxa = hora.numdias
          casa.tempotaxa = 1
          casa.objmudou = 1
        fimse
        ret arg0.msg(txtcopiamai(p.descnome, "A") + " informa que a taxa foi paga.")
      efim
      !mudou && t.addfim("Você não deve nada.")
    fimse
  efim
  arg0.msg("Não tem como você pagar isso aqui.")
