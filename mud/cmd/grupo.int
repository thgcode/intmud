classe cmd_grupo
herda comando_comum
const posic = 5
const txtajuda = "\b\c3Grupo\b\n\
Sintaxe: GRUPO <personagem>\n\
         GRUPO +\n\
         GRUPO -\n\
         GRUPO\n\
Permite que dois ou mais jogadores de níveis próximos trabalhem em grupo,\n\
dividindo a experiência ganha em batalhas.\n\
Quando alguém do grupo é atacado os outros também entram na luta.\n\
Os membros do grupo podem ainda conversar entre si com o comando GCHAT.\n\
Na movimentação pelo MUD, o grupo é semelhante aos seus integrantes\n\
seguindo o líder.\n\n\
Digitando GRUPO e o nome de um personagem adiciona no seu grupo\n\
o personagem que está seguindo você. Se já estava adicionado, remove.\n\
Um personagem também pode deixar o grupo digitando SEGUIR.\n\
GRUPO + adiciona todos que estão seguindo você.\n\
GRUPO - encerra o grupo fazendo os membros pararem de seguir o líder.\n\
Digitar GRUPO sem nenhum argumento mostra as estatísticas de cada membro\n\
do grupo.\n\
Vide também os comandos: \c2SEGUIR\b  \c2GCHAT\b"
uint16 nivelmax # Nível máximo para entrar no grupo; vide função nivelgrupo
uint16 nivelmin # Nível mínimo para entrar no grupo; vide função nivelgrupo
#
const m_encerrado = "Grupo encerrado."
const m_nenhum_grupo = "Você não está em nenhum grupo."
const m_nao_lider = "Você não é $s líder do grupo."
const m_lutando = "Você está lutando."
const m_nivel_baixo = "$A está abaixo do nível $m."
const m_nivel_alto = "$A está acima do nível $m."
const m_nome_grupo = "Grupo $A"
const m_nao_adicionar = "$A não pode ser adicionad$t."
const m_adicionado = "Adicionado no grupo: $m"
const m_removido = "Removido do grupo: $m"
const m_ninguem_adicionado = "Nenhum personagem adicionado no grupo."

func escr
  listaobj l
# Encerrar o grupo
  se arg1 == "-"
    se arg0.tseguir == 2
      $mens.p(arg0)
      epara l.addfim(arg0.lseguir), l, l.ini.remove
        continuar l.objini.tseguir != 1
        l.objini.iseguir.remove
        l.objini.tseguir = 0
        $mens.msg(m_encerrado, l.objini.iseguir)
      efim
      arg0.tseguir = 0
      $mens.msg(m_encerrado, arg0)
      ret
    senao arg0.iseguir && arg0.tseguir == 1
      ret $mens.mp(m_nao_lider, arg0)
    senao
      ret $mens.mp(m_nenhum_grupo, arg0)
    fimse
# Adicionar quem está seguindo no grupo
  senao arg1 == "+"
    se arg0.iseguir && arg0.tseguir == 1
      ret $mens.mp(m_nao_lider, arg0)
    senao arg0.atkenv
      ret $mens.mp(m_lutando, arg0)
    fimse
    nivelgrupo(arg0)
    txt200 lin
    epara l.addfim(arg0.lseguir), l, l.ini.remove
      refvar obj = l.objini
      se obj.tseguir
      senao obj.pnivel < nivelmin
        $mens.mens = nivelmin
        $mens.mp(m_nivel_baixo, arg0, obj)
      senao obj.pnivel > nivelmax
        $mens.mens = nivelmax
        $mens.mp(m_nivel_alto, arg0, obj)
      senao
        obj.tseguir = 1
        lin += ", " + txtcopiamai(obj.nome, "A")
        nivelgrupo(arg0)
      fimse
    efim
    ret !lin, $mens.mp(m_ninguem_adicionado, arg0)
    arg0.tseguir = 2
    msggrupo(arg0, m_adicionado, txt2(lin))
    ret
# Adicionar um personagem no grupo
  senao arg1
    se arg0.iseguir && arg0.tseguir == 1
      ret $mens.mp(m_nao_lider, arg0)
    senao arg0.atkenv
      ret $mens.mp(m_lutando, arg0)
    fimse
    nomeobj nome
    nome.ini(arg1, 1)
    epara l.addfim(arg0.lseguir), l && !nome.nome(l.objini.ident), l.ini.remove
    efim
    refvar obj = ref(l.objini)
    se !obj
      $mens.mens = arg1
      ret $mens.mp("Você não vê $m.", arg0)
    senao int(l.objini.tseguir) >= 2
      ret $mens.mp(m_nao_adicionar, arg0, obj)
    fimse
    nivelgrupo(arg0)
    se obj.tseguir
      msggrupo(arg0, m_removido, obj.nome)
      obj.tseguir = 0
    senao obj.pnivel < nivelmin
      $mens.mens = nivelmin
      $mens.mp(m_nivel_baixo, arg0, obj)
    senao obj.pnivel > nivelmax
      $mens.mens = nivelmax
      $mens.mp(m_nivel_alto, arg0, obj)
    senao
      obj.tseguir = 1, arg0.tseguir = 2
      msggrupo(arg0, m_adicionado, obj.nome)
    fimse
    ret
# Informações sobre os integrantes do grupo
  senao
    ref lider
    se arg0.tseguir == 2
      lider = arg0
    senao arg0.iseguir && arg0.tseguir == 1
      lider = arg0.iseguir.objlista
    fimse
    ret !lider, $mens.mp(m_nenhum_grupo, arg0)
    $mens.mp(m_nome_grupo, arg0, lider)
    arg0.msg(info(lider))
    epara l.addfim(lider.lseguir), l, l.ini.remove
      l.objini.tseguir == 1 && arg0.msg(info(l.objini))
    efim
    ret
  fimse

func info # Retorna informações de um personagem
# arg0 = personagem
  txt100 lin
  lin = txtcopiamai(arg0.nome, "A")
  lin += " Vida " + arg0.pvida + "/" + arg0.pvidamax
  lin += "  Mana " + arg0.pmana + "/" + arg0.pmanamax
  lin += "  Vigor " + arg0.pmove + "/" + arg0.pmovemax
  lin += "  Nível " + arg0.pnivel
  ret lin

func msggrupo # Envia mensagem para todos do grupo
# arg0 = personagem líder do grupo
# arg1 = mensagem
# arg2 = $m na mensagem
  refvar mens = txttroca(arg1, "$m", arg2)
  arg0.msg(mens)
  listaobj l
  epara l.addfim(arg0.lseguir), l, l.ini.remove
    l.objini.tseguir == 1 && l.objini.msg(mens)
  efim

func nivelgrupo # Acerta variáveis nivelmin e nivelmax
# arg0 = personagem líder do grupo
  nivelmin = nivelmax = arg0.pnivel
  listaitem i
  epara i = arg0.lseguir.ini, i, i.depois
    se i.obj.tseguir != 1
    senao nivelmin > i.obj.pnivel
      nivelmin = i.obj.pnivel
    senao nivelmax < i.obj.pnivel
      nivelmax = i.obj.pnivel
    fimse
  efim
  refvar dif = intpos(config:gruponivel - (nivelmax - nivelmin))
  nivelmax += dif, nivelmin -= dif


classe cmd_gchat
herda comando_comum
const objnivel = 4
const posic = 0
const txtajuda = "\b\c3GChat\b\n\
Sintaxe: GCHAT <mensagem>\n\
Envia uma mensagem para todos os integrantes do seu grupo.\n\
Vide também: GRUPO"

func escr
  ref lider
  se arg0.tseguir == 2
    lider = arg0
  senao arg0.iseguir && arg0.tseguir == 1
    lider = arg0.iseguir.objlista
  fimse
  ret !lider, arg0.msg("Você não está em nenhum grupo.")
  $mens.mens = arg1
  $mens.p(arg0)
  $mens.msginv(config:gchatmsg, lider)
  listaobj l
  epara l.addfim(lider.lseguir), l, l.ini.remove
    l.objini.tseguir == 1 && $mens.msginv(config:gchatmsg, l.objini)
  efim
