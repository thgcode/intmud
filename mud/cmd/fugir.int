classe cmd_fugir
herda comando_comum
const posic = 7
const txtajuda = "\b\c3Fugir\b\n\
Sintaxe: FUGIR [número]\n\
Tenta fugir de uma batalha.\n\
Seguido de um número, muda os pontos para a fuga. Quando os pontos\n\
de vida do seu personagem estiverem abaixo desse número, ele tentará\n\
fugir automaticamente.\n\
Para saber quantos pontos foram definidos, tecle fugir fora da batalha."
#
const m_pontos_invalidos = "Pontos de vida inválidos: $m"
const m_pontos_mudar = "Pontos de vida para fugir mudaram para: $m"
const m_pontos_ver = "Pontos de vida para fugir: $m"
const m_folego = "Você está sem fôlego."
const m_falha1 = "$P tenta fugir, mas não consegue."
const m_falha2 = m_falha1
const m_sem_saida1 = "$P tenta fugir mas não vê para onde."
const m_sem_saida2 = m_sem_saida1

func escr
# Checa posição
  se arg1
    $mens.mens = arg1
    ret txt(intpos(arg1)) != arg1, $mens.mp(m_pontos_invalidos, arg0)
    arg0.p_fugir = arg1
    ret $mens.mp(m_pontos_mudar, arg0)
  senao arg0.posicao != 7
    $mens.mens = arg0.p_fugir
    ret $mens.mp(m_pontos_ver, arg0)
  senao
    ret arg0.pmove < 5 && !arg0.j_imortal, $mens.mp(m_folego, arg0)
    arg0.p_espera = 5
    fugir(arg0)
  fimse

func fugir # Tenta fugir da luta
# arg0 = personagem
# Gera eventos
  listaobj l
  epara l.addfim(arg0.evento, r.evento), l, l.ini.remove
    ret l.objini.cmd_fugir(arg0), nulo
  efim
# Checa se consegue fugir
  real2 valor
  listaitem i
  epara i = arg0.atkrec.ini, i, i.depois
    valor += i.obj.persobat.precisao
  efim
  valor = valor * rand(70, 130) - arg0.persobat.evasao * 150
  se valor > 0 && !arg0.j_imortal
    $mens.p(arg0)
    $mens.mens = ""
    $mens.mtodos2(m_falha1, m_falha2)
    ret
  fimse
# Obtém direção para onde fugir
  refvar dir = $comando_mover.fugirdir(arg0)
  refvar r = ref(arg0.dono.dir_[dir](arg0))
  se !r || r == arg0.dono || !dir
    $mens.p(arg0)
    $mens.mens = ""
    $mens.mtodos2(m_sem_saida1, m_sem_saida2)
    ret
  fimse
# Foge
  arg0.pmove -= 5
  arg0.batalhafim
  $mens.p(arg0)
  $mens.mtodos1($cmd_[dir].m_fugir)
  arg0.mudadono(r)
  $mens.mens = ""
  !(arg0.bitver & 256) && $mens.mvis2("", $cmd_[dir].m_entra1)
