classe cmd_dormir
herda comando_comum
const posic = 5
const txtajuda = "\b\c3Dormir\b\n\
Sintaxe: DORMIR [objeto]\n\
Dorme no chão. Com o nome de um objeto, tenta dormir no objeto."

func escr
  ref r
# Checa posição
  ret arg0.bitver & 0x800, arg0.msg("Você não consegue dormir.")
  casovar arg0.posicao
  casose "5" # Descansando
  casose "6" # Sentado
    r = arg0.poslugar.objlista
    sair
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msg(arg0.erropos)
  casofim
# Obtém aonde quer dormir
  se arg1
    listaitem i # Para procurar o item
    nomeobj n # Para reconhecer os itens
    n.ini(arg1, 1)
    epara i = arg0.dono.dentro1.ini, i, i.depois
      sair i.obj.visivel(arg0) && n.nome(i.obj.ident, i.obj.objtot)
    efim
    ret r && r != i.obj, $mens.mp("\c9Talvez você deva se levantar primeiro.\b", arg0)
    r = i.obj
  fimse
  listaobj l
# Dormir em algum objeto
  se r
    casovar r.i_movel(arg0)
    casose
      ret $mens.mp("Não é possível dormir $e $o.", arg0, nulo, r)
    casose "2"
    casose "4"
    casofim
    se r != arg0.poslugar.objlista && r.i_lugar <= r.persolugar.total
      ret $mens.mp("Não cabe mais ninguém $e $o.", arg0, nulo, r)
    fimse
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_dormirobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_dormirobj(arg0, r, r), nulo
    efim
    $mens.p(arg0, nulo, r)
    $mens.mtodos1("$P deita $e $o e dorme.")
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 4
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_dormiuobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_dormiuobj(arg0, r, r)
    efim
# Dormir num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp("Você não vê $m.", arg0)
# Dormir no chão
  senao
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_dormirchao(arg0), nulo
    efim
    $mens.p(arg0)
    $mens.mtodos1("$P deita e dorme.")
    arg0.poslugar.remove, arg0.posicao = 4
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_dormiuchao(arg0)
    efim
  fimse


classe cmd_acordar
herda comando_comum
const posic = 4
const txtajuda = "\b\c3Acordar\b\n\
Sintaxe: ACORDAR [personagem]\n\
Acorda. Com o nome de um personagem, tenta acordar o personagem."

func escr
# Acordar outro personagem
  se arg1
    ret arg0.posicao == 4, arg0.msg(arg0.erropos)
    listaitem i # Para procurar o item
    nomeobj n # Para reconhecer os itens
    n.ini(arg1, 1)
    epara i = arg0.dono.dentro2.ini, i, i.depois
      sair i.obj.visivel(arg0) && n.nome(i.obj.ident, i.obj.objtot)
    efim
    ref r
    r = i.obj
    se !r
      $mens.mens = arg1
      $mens.mp("Você não vê $m.", arg0)
      ret
    senao r.posicao != 4
      ret $mens.mp("$A não está dormindo.", arg0, r)
    senao r.bitver & 0xC00 == 0x400
      $mens.p(arg0, r)
      $mens.mtodos1("$P tenta acordar $a.")
      ret
    fimse
    listaobj l
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(r, arg0, r), nulo
    efim
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(r, arg0, arg0), nulo
    efim
    r.posicao = 5
    $mens.p(arg0, r)
    $mens.mtodos1("$P acorda $a.")
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_acordou(r, arg0, r)
    efim
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_acordou(r, arg0, arg0)
    efim
# Acordar o próprio personagem
  senao arg0.posicao > 4
    ret $mens.mp("\c9Mas você já está acordad$s.\b", arg0)
  senao arg0.bitver & 0xC00 == 0x400
    ret $mens.mp("Você não consegue acordar.", arg0, r)
  senao
    listaobj l
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(arg0, nulo, arg0), nulo
    efim
    arg0.posicao = 5
    $mens.p(arg0)
    $mens.mtodos1("$P acorda.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_acordou(arg0, nulo, arg0)
    efim
  fimse
