classe cmd_carregar
herda comando_comum
const txtajuda = "\b\c3Carregar\b\n\
Sintaxe: CARREGAR\n\
         CARREGAR <objeto>\n\
Carrega a arma que você estiver empunhando com um objeto que você está\n\
carregando. Sem argumentos você verifica se a arma está carregada\n\
e com o quê."
const posic = 5
#
const m_sem_arma = "Você não está empunhando nenhuma arma."
const m_sem_municao = "$O não usa munição"
const m_descarregado = "$O está descarregad$u."
const m_carregado = "$O está carregad$u com:"
const m_conhecimento = "Não possui conhecimento suficiente em $m."
const m_carrega_um1 = "Você carrega $a com $o."
const m_carrega_um2 = "$P carrega $a com $o."
const m_carrega_mais1 = "Você carrega $a com $mx $o."
const m_carrega_mais2 = "$P carrega $a com $mx $o."
#
const m_nao_pode = "Não é possível carregar $o com $n."
const m_cheio = "Não consegue carregar $o com mais nada."
const m_espaco = "Não há espaço suficiente para carregar $o com $n."
const m_nivel = "Você precisa chegar no nível $m para carregar $o com $n."
const m_renascer1 = "Você precisa renascer uma vez para carregar com $n."
const m_renascer2 = "Você precisa renascer duas vezes para carregar com $n."
const m_renascer3 = "Você precisa renascer $m vezes para carregar com $n."

func escr
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  refvar arma = ref(arg0.iempu.obj)
  se !arma
    $mens.mp(m_sem_arma, arg0)
  senao !arma.armatipo || int(arma.armamunicao) != 2
    $mens.mp(m_sem_municao, arg0, nulo, arma)
  senao !arg1
    textotxt t
    listaitem item
    epara item = arma.dentro1.ini, item, item.depois
      continuar !item.obj.visivel(arg0)
      t.addfim(txt(item.obj.objtot) + " " + txtcopiamai(item.obj.descnome, "A"))
    efim
    se !t.linhas
      $mens.mp(m_descarregado, arg0, nulo, arma)
    senao
      $mens.p(arg0, nulo, arma)
      t.juntalin("(", "x)")
      t.addini($mens.txt(m_carregado, arg0))
      arg0.msg2(t.remove(1000))
    fimse
  senao !arma.armacarregar
    $mens.p(arg0, nulo, arma)
    $mens.msg("$O não pode ser carregad$u.", arg0)
  senao
    refvar tipo = txtsublin(config:arma_txt, arma.armatipo, 1)
    se arg0.var.h_[tipo]_ < arma.armacarregar
      $mens.mens = tipo
      $mens.mp(m_conhecimento, arg0, nulo, arma)
    senao
      nomeobj n # Para reconhecer os itens
      listaobj l
      txt100 erro # Mensagem de erro, no caso de não ter carregado
      int8 achou # Se carregou com algum item
      n.ini(arg1, misc:objmax, misc:tudo)
      epara l.addfim(arg0.dentro1), l, l.ini.remove
        continuar !l.objini.visivel(arg0) || !n.nome(l.objini.ident, l.objini.objtot)
        refvar e = carregar(arg0, arma, l.objini, int(n))
        e ? (erro = e) : achou++
        sair achou >= 10
      efim
      se achou
      senao erro
        arg0.msg(erro)
      senao
        $mens.mens = arg1
        $mens.mp("Você não vê $m.", arg0)
      fimse
    fimse
  fimse

func carregar # Carrega arma com um item
# arg0=personagem, arg1=arma, arg2=item, arg3=quantidade de itens
# Retorna mensagem de erro ou nulo se carregou
  se txtproclin(arg1.i_municao, txt(arg2, 2)) < 0
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_nao_pode, arg0)
  senao arg1.dentro1.total + (arg2.item == 2 ? 1 : arg3) > arg1.objmax
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_cheio, arg0)
  senao arg1.volden + arg2.voltot * arg3 / arg2.objtot > arg1.volmax
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_espaco, arg0)
  senao int(arg2.pnivel) > arg0.pnivel
    $mens.mens = int(arg2.pnivel)
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_nivel, arg0)
  senao int(arg2.renascer) > arg0.prenascer
    $mens.mens = int(arg2.renascer)
    $mens.p(arg0, nulo, arg1, arg2)
    se arg2.renascer == 1
      ret $mens.txt(m_renascer1, arg0)
    senao arg2.renascer == 2
      ret $mens.txt(m_renascer2, arg0)
    fimse
    ret $mens.txt(m_renascer3, arg0)
  fimse
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg2), nulo
  efim
  $mens.p(arg0, arg1, arg2)
  se arg3 <= 1
    $mens.mvis2(m_carrega_um1, m_carrega_um2)
  senao
    $mens.mvis2(m_carrega_mais1, m_carrega_mais2)
  fimse
  ref r = arg2.mudadono(arg1, arg3)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, arg1)
  efim
  epara e.addfim(r.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, r)
  efim
  arg0.p_espera += 2
