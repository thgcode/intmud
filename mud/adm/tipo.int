classe adm_tipo
const iniclasse = !$[arg0] && criar(arg0)
const txtajuda = "\b\c3Comandos para tipos de personagens (raças e classes)\b\n\
  \c2LTIPO\b     Lista tipos de personagens\n\
  \c2CTIPO\b     Cria um tipo de personagem\n\
  \c2ETIPO\b     Edita um tipo de personagem\n\
  \c2ATIPO\b     Apaga um tipo de personagem\n\
  \c2TRES\b      Edita comandos do reset de tipo"


classe adm_ltipo
herda comando_adm
const txtajuda = "\b\c3Ltipo\b\n\
Sintaxe: LTIPO [nome]\n\
Lista os tipos de personagens que começam com o nome especificado.\n\
Se o nome for omitido, lista todos os personagens.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  txt100 lin
  se !p.iniclasse("t_" + arg1)
    ret arg0.msg("\bNenhum tipo encontrado.")
  fimse
  refvar txtmais = arg0.sock.txtmais
  txtmais.limpar
  lin = "Menu raças:\n  "
  epara nulo, p.lin, p.depois
    continuar $[p.texto].menu != 1
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Menu classes:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu != 2
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Menu subclasses:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu != 3
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Em nenhum menu:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  arg0.sock.txtmostra


classe adm_ctipo
herda comando_adm
const txtajuda = "\b\c3Ctipo\b\n\
Sintaxe: CTIPO <nome>\n\
Cria um tipo de personagem com o nome especificado.\n\
O nome deve ser composto de letras, mas pode ter espaços no meio.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  txt80 nome
  uint16 num
# Checa se digitou nome do tipo
  se arg1 == ""
    ret arg0.msg("Tecle o nome do tipo após CTIPO")
  fimse
# Checa se nome válido
  nome = txte(txtremove(arg1, "EMDSA"))
  se txtsepara(nome, "O+DOL,DOL+O") != nome
    ret arg0.msg("Nome inválido para tipo de personagem: " + nome)
  senao p.existe("t_" + nome)
    ret arg0.msg("Já existe o tipo: " + nome)
  fimse
# Cria tipo
  txt200 lin
  lin = "t_" + txts(nome) + "\nconfig/tipo\nherda comum_tipo"
  lin += "\nconst nome = \"" + txtcopiamai(txte(nome), "A") + "\""
  p.criar(lin)
  criar("admordena", "config/tipo")
# config:salvar
  arg0.msg("\bTipo criado: " + nome)
  admlog(arg0, "criou tipo " + nome)


classe adm_atipo
herda comando_adm
const txtajuda = "\b\c3Atipo\b\n\
Sintaxe: ATIPO <nome>\n\
Apaga o tipo de personagem com o nome especificado.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle um nome após ATIPO")
  senao !p.existe("t_" + arg1)
    arg0.msg("Tipo não existe: " + arg1)
  senao
    arg0.sock.tpasso = arg1
    passo(arg0.sock, "")
  fimse

func passo # Confirmar que quer apagar tipo
  arg0.passo = ""
  se arg1 == "n"
    arg0.msg("Cancelado")
  senao arg1 == "s"
    prog p
    p.apagar("t_" + arg0.tpasso)
    config:salvar
    arg0.msg("Tipo apagado: " + arg0.tpasso)
    admlog(arg0, "apagou tipo " + arg0.tpasso)
  senao
    arg0.passo = este
    arg0.msg("Apagar tipo " + arg0.tpasso + "? Tecle S ou N")
  fimse
  ret 1


classe adm_etipo
herda comando_adm
const txtajuda = "\b\c3Etipo\b\n\
Sintaxe: ETIPO <nome>\n\
Edita o tipo de personagem com o nome especificado.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle o nome do tipo após ETIPO")
  senao !p.existe("t_" + txts(arg1))
    arg0.msg("Tipo não existe: " + arg1)
  senao
    arg0.sock.var.classe1 = "t_" + txts(txtmin(arg1))
    arg0.sock.m_menu("menu_tipo")
  fimse


classe menu_tipo
herda comando_menu
const titulo = "Tipo " + txt(var.classe1, 2)
const colunas = 25
#
const nome1 = "Texto do menu"
const tipo1 = "opc_linha"
const clas1 = var.classe1
const vari1 = "nome"
#
const nome2 = "Texto comando Quem"
const tipo2 = "opc_linha"
const clas2 = var.classe1
const vari2 = "nomequem"
#
const nome3 = "Descrição"
const info3 = "Aparece quando o jogador escolhe raça ou classe"
const tipo3 = "opc_texto"
const clas3 = var.classe1
const vari3 = "desc"
#
const nome4 = "Está no menu"
const tipo4 = "opc_opcao"
const clas4 = var.classe1
const vari4 = "menu"
const extr4 = "Nenhum menu\nRaças\nClasses\nSubclasses"
#
const nome5 = "Somente sexo"
const tipo5 = "opc_opcao"
const clas5 = var.classe1
const vari5 = "sexo"
const extr5 = "Todos\nFeminino\nMasculino"
#
const nome6 = "Somente raças"
const info6 = "Se esse tipo for uma classe ou subclasse que não existe em todas as raças,\n\
escreva aqui em quais raças existe"
const tipo6 = "opc_tipos"
const clas6 = var.classe1
const vari6 = "racas"
#
const nome7 = "Somente classes"
const info7 = "Se esse tipo for uma subclasse que não existe em todas as classes,\n\
escreva aqui em quais classes existe"
const tipo7 = "opc_tipos"
const clas7 = var.classe1
const vari7 = "classes"
#
const nome8 = "Atributos números"
const tipo8 = "opc_atribnum"
const clas8 = var.classe1
const vari8 = "recalc_auto"
const exec8 = "recalc_ajusta"
#
const nome9 = "Atributos sim/não"
const tipo9 = "opc_atribbit"
const clas9 = var.classe1
const vari9 = "recalc_auto"
const exec9 = "recalc_ajusta"
#
const nome10 = "Atributos ataque"
const info10 = "Afetam os danos quando o personagem ataca alguém"
const tipo10 = "opc_atribatk"
const clas10 = var.classe1
const vari10 = "recalc_auto"
const exec10 = "recalc_ajusta"
#
const nome11 = "Atributos defesa"
const info11 = "Afetam os danos quando alguém ataca o personagem"
const tipo11 = "opc_atribdef"
const clas11 = var.classe1
const vari11 = "recalc_auto"
const exec11 = "recalc_ajusta"
#
const nome12 = "Equip posições"
const info12 = "Se não for <nulo>, são as posições aonde pode usar equipamentos"
const tipo12 = "opc_vetorhex"
const clas12 = var.classe1
const vari12 = "equip1"
const extr12 = config:equip_txt
#
const nome13 = "Equip adicionar"
const info13 = "Adicionar essas posições no personagem"
const tipo13 = "opc_vetorhex"
const clas13 = var.classe1
const vari13 = "equip2"
const extr13 = config:equip_txt
#
const nome14 = "Equip remover"
const info14 = "Remover essas posições do personagem"
const tipo14 = "opc_vetorhex"
const clas14 = var.classe1
const vari14 = "equip3"
const extr14 = config:equip_txt
#
const nome15 = "Exp máx fator 0"
const info15 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo15 = "opc_numreal--"
const clas15 = var.classe1
const vari15 = "expmax0"
#
const nome16 = "Exp máx fator 1"
const info16 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo16 = "opc_numreal--"
const clas16 = var.classe1
const vari16 = "expmax1"
#
const nome17 = "Exp máx fator 2"
const info17 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo17 = "opc_numreal--"
const clas17 = var.classe1
const vari17 = "expmax2"
#
const nome18 = "Exp máx cada nível"
const info18 = "Permite definir a experiência para subir um nível em níveis específicos"
const tipo18 = "opc_nivel--"
const clas18 = var.classe1
const vari18 = "expmax_"
#
const titu19 = "* Somente jogadores"
#
const nome19 = "Nível mínimo"
const info19 = "Quando o personagem é criado ou renasce, ele vai para esse nível.\n\
Se houverem níveis diferentes na raça, classe e subclasse, é considerado\n\
somente o maior."
const tipo19 = "opc_numero"
const clas19 = var.classe1
const vari19 = "nmin"
const extr19 = "0 65535"
#
const nome20 = "Nível máximo"
const info20 = "Quando o personagem chega no nível máximo, não sobe mais de nível, mas pode\n\
renascer. Se houverem níveis diferentes na raça, classe e subclasse,\n\
é considerado somente o maior. Se os três forem 0, não renasce."
const tipo20 = "opc_numero"
const clas20 = var.classe1
const vari20 = "nmax"
const extr20 = "0 65535"
#
const nome21 = "Evolui para"
const tipo21 = "opc_tipos"
const info21 = "Os novos tipos que o personagem pode escolher no comando renascer"
const clas21 = var.classe1
const vari21 = "evoluir"
#
const nome22 = "Fome e sede"
const info22 = "Somente raças; se pode sentir fome e sede (se não for imortal)"
const tipo22 = "opc_simnao"
const clas22 = var.classe1
const vari22 = "fome"
#
const nome23 = "Ataque automático"
const info23 = "Habilidade usada em ataques automáticos sem arma"
const tipo23 = "opc_linha"
const clas23 = var.classe1
const vari23 = "atipo"
#
const nome24 = "Volume mínimo"
const info24 = "Somente raças; o volume é escolhido aleatoriamente"
const tipo24 = "opc_numero"
const clas24 = var.classe1
const vari24 = "volmin"
const exec24 = "jogador:ajustajog"
#
const nome25 = "Volume máximo"
const info25 = "Somente raças; o volume é escolhido aleatoriamente"
const tipo25 = "opc_numero"
const clas25 = var.classe1
const vari25 = "volmax"
const exec25 = "jogador:ajustajog"
#
const nome26 = "Peso mínimo"
const info26 = "Somente raças; o peso é escolhido aleatoriamente"
const tipo26 = "opc_numero"
const clas26 = var.classe1
const vari26 = "pesomin"
const exec26 = "jogador:ajustajog"
#
const nome27 = "Peso máximo"
const info27 = "Somente raças; o peso é escolhido aleatoriamente"
const tipo27 = "opc_numero"
const clas27 = var.classe1
const vari27 = "pesomax"
const exec27 = "jogador:ajustajog"
#
const nome50 = "Variáveis"
const tipo50 = "opc_linha"
const clas50 = var.classe1
const vari50 = "inivar"


classe comum_tipo
#
# Ordem que deve organizar as constantes:
const admordem = "nome nomequem menu desc sexo racas classes\n\
equip1 equip2 equip3 nmin nmax evoluir fome atipo\n\
volmin volmax pesomin pesomax inivar"
const iniclasse = !$[arg0] && criar(arg0)
const fome = 1

func recalc_ajusta # Ajusta variáveis quando algum atributo foi mudado
  refvar nome = txt2(txte(este))
  prog p
  ref r
  epara r = $jogador, r, r = objdepois(r)
    (r.tipo1 == nome || r.tipo2 == nome || r.tipo3 == nome) && (r.recalc = 1)
  efim
  epara p.iniclasse("p_"), p.lin, p.depois
    epara r = $[p.texto], r, r = objdepois(r)
      (r.tipo1 == nome || r.tipo2 == nome || r.tipo3 == nome) && (r.recalc = 1)
    efim
  efim
